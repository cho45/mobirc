#!/usr/bin/perl
use strict;
use warnings;
use utf8;
use POE;
use Encode;
use File::Spec;
use FindBin;
use Carp;
use Getopt::Long;

use lib File::Spec->catfile( $FindBin::Bin, 'lib');

use Mobirc;
use Mobirc::Util;
use Mobirc::HTTPD;
use Mobirc::IRCClient;
use Mobirc::ConfigLoader;

$SIG{INT} = sub { die "SIGINT!\n" };

my $daemonize_fg = false;
my $conffname = File::Spec->catfile($FindBin::Bin, 'config.yaml');
my $version = false;
GetOptions(
    'daemonize' => \$daemonize_fg,
    'config=s'  => \$conffname,
    'version'   => \$version,
) or die "Usage: $0 -c config.yaml";
Getopt::Long::Configure("bundling"); # allows -c -v

if ($version) {
    print "Mobirc/$Mobirc::VERSION\n";
    exit;
}

die "file does not exist: $conffname" unless -f $conffname;

DEBUG "load config: $conffname";
my $config = Mobirc::ConfigLoader->load($conffname);

# set default vars.
$config->{irc}->{ping_delay} ||= 30;
$config->{irc}->{reconnect_delay} ||= 10;
$config->{httpd}->{charset} ||= 'cp932';
$config->{httpd}->{root} ||= '/';
$config->{httpd}->{echo} = true unless exists $config->{httpd}->{echo};
$config->{global}->{assets_dir} ||= File::Spec->catfile($FindBin::Bin, 'assets');
$config->{httpd}->{cookie_expires} ||= '+3d';
$config->{httpd}->{content_type} ||= 'text/html; charset=Shift_JIS';

# daemonize
if ( $daemonize_fg ) {
    daemonize($config->{global}->{pid_fname});
}

Mobirc::IRCClient->init($config);
Mobirc::HTTPD->init($config);
$poe_kernel->run();

exit 0;

__END__

=head1 SYNOPSIS

   mobirc config.yaml

=head1 WARNINGS

This program is still ALPHA QUALITY. Don't use at production environment.

=head1 CONCEPT

easy hack, easy maintenance, modern style, easy to use for perl hackers.

=head1 DONE

    use Encode instead of Unicode::Japanese
    rebirth channel list
    internal string should be flagged utf8
    Perl critic clean
    show_channel should use TT.
    split irc client feature to IRCClient.pm instead of keitaric.pl
    new name.
    use YAML instead of AppConfig
    dispatcher likes Sledge(HTTPD)
    TT should in the assets dir?
    cool uri
    use TT at dispatch_index
    templates should be configurable
    use poe's heap instead of global variables ;-(
    Makefile.PL

=head1 TODO

    avoid warnings.

=head1 supported phones

  au WIN
  docomo foma

that's all...

=head1 HISTORY

mobirc is based on 'keitairc,v 1.33 2007/10/16 23:44:55 morimoto'.

=head1 LICENSE

This program is covered by the GNU General Public License 2

